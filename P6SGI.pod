=NAME P6SGI - Perl 6 Web Server Gateway Interface Specification

=begin pod

=head1 STATUS

This is a Proposed Draft.

Version 0.6.Draft

=head1 0 INTRODUCTION

This document standardizes the interface to be implemented by web application developers in Perl 6. It provides a standard protocol by which application servers may communicate with web applications.

This standard has the following goals:

=item Standardize the interface between server and application so that web developers may focus on application development rather than the nuances of supporting each of several server platforms.

=item Keep the interface simple so that a web application or middleware requires no additional tools or libraries other than what exists in a standard Perl 6 environment, no module installations are required.

=item Keep the interface simple so that servers and middleware are simple to implement.

=item Allow the interface to flexible enough to accomodate a variety of common use-cases and simple optimzations.

=item Provide flexibility so that unanticipated use-cases may be implemented and so that the interface may be extended by servers wishing to do so.

Aside from that is the underlying assumption that this is a simple interface and ought to at least somewhat resemble work in the standards it is derived from, including [Rack](http://www.rubydoc.info/github/rack/rack/master/file/SPEC), [WSGI](https://www.python.org/dev/peps/pep-0333/), [PSGI](https://metacpan.com/pod/PSGI), [CGI](http://www.w3.org/CGI/), and others.

=head1 1 TERMINOLOGY

A D<P6SGI application> is a Perl 6 routine that expects to receive an environment from an I<application server> and returns a response each time it is called by the server.

A D<Web Server> is an application that processes requests and responses according to the HTTP or related protocol.

The D<origin> is the external entity that makes a given request and/or expects a response from the application server. This can be thought of generically as a web browser, bot, or other user agent.

An D<application server> is a program that is able to provide an environment to a I<P6SGI application> and process the value returned from such an application.

The I<application server> might be associated with a I<web server>, might itself be a I<web server>, might process a protocol used to communicate with a I<web server> (such as CGI or FastCGI), or may be something else entirely not related to a I<web server> (such as a tool for testing I<P6SGI applications>).

D<Middleware> is a I<P6SGI application> that wraps another I<P6SGI application> for the purpose of performing some auxiliary task such as preprocessing request environments, logging, postprocessing responses, etc.

A D<framework developer> is a developer who writes an I<application server>.

An D<application developer> is a developer who writes a I<P6SGI application>.

=head1 2 SPECIFICATION

This specification is divided into three layers:

=item Layer 0: Server

=item Layer 1: Middleware

=item Layer 2: Application

Each layer has a specific role related the other layers. The server layer is primarily responsible for managing the application lifecycle and performing the low-level protocol handling aspects. The application layer is primarily responsible for receiving metadata and content from the server and delivering metadata and content back to the server. The middleware layer is responsible for enhancing the application or server by providing additional services and utilities.

This specification goes through each layer in order.

=head2 2.0 Layer 0: Server

A P6SGI application server is a program capable of running P6SGI applications as defined by this specification.

A P6SGI application server typically implements some variant of web service. This typically means implementing an HTTP/1.x protocol service or a related protocol such as CGI, FastCGI, SCGI, etc. An application server also manages the application lifecycle and executes the application, providing it with a complete environment, and processing the response from the application to determine how to respond to the origin.

An application server SHOULD strive to be as flexible as possible to allow as many unusual interactions, subprotocols, and upgrade protocols to be implemented as possible within the connection.

=head3 2.0.0 Locating Applications

The server MUST be able to find applications.

It SHOULD be able to load applications found in P6SGI script files. These are Perl 6 code files that end with the definition of a block to be used as the application routine. For example:

    use v6;
    sub app(%env) {
        start {
            200, [ Content-Type => 'text/plain' ], [ 'Hello World!' ]
        }
    }

These files MAY have a F<.p6w> suffix, but this is not at all required.

=head3 2.0.1 The Environment

The environment MUST be an L<Associative>. The keys of this map are mostly derived the old Common Gateway Interface (CGI) as well as a number of additional P6SGI-specific values. The application server MUST provide each key as the type given. All variables given in the table below MUST be supported, except for those with the C<p6sgix.> prefix.

=begin table :allow<C>

    Variable                 | Constraint                             | Description
    =========================|========================================|======================================
    C<REQUEST_METHOD>        | C<< Str:D where *.chars > 0 >>         | The HTTP request method, such as
                             |                                        | "GET" or "POST".

    C<SCRIPT_NAME>           | C<< Str:D where any('', m{ ^ "/" }) >> | This is the initial prtion of the
                             |                                        | URL path that refers to the
                             |                                        | application.

    C<PATH_INFO>             | C<< Str:D where any('', m{ ^ "/" }) >> | This is the remainder of the request
                             |                                        | URL path within the application.
                             |                                        | This value SHOULD be URI decoded by
                             |                                        | the application server according to
                             |                                        | L<RFC 3875|http://www.ietf.org/rfc/rfc3875>

    C<REQUEST_URI>           | C<< Str:D >>                           | This is the exact URL sent by the
                             |                                        | client in the request line of the
                             |                                        | HTTP request. The application server
                             |                                        | SHOULD NOT perform any decoding on
                             |                                        | it.

    C<QUERY_STRING>          | C<< Str:D >>                           | This is the portion of the requested
                             |                                        | URL following the C<?>, if any.

    C<SERVER_NAME>           | C<< Str:D where *.chars > 0 >>         | This is the server name of the web
                             |                                        | server.

    C<SERVER_PORT>           | C<< Int:D where * > 0 >>               | This is the server port of the web
                             |                                        | server.

    C<SERVER_PROTOCOL>       | C<< Str:D where *.chars > 0 >>         | This is the server protocol sent by
                             |                                        | the client. It MAY be set to "HTTP/1.0"
                             |                                        | or "HTTP/1.1" or "HTTP/2" or
                             |                                        | "WebSocket/13" or a similar value.

    C<CONTENT_LENGTH>        | C<< Int:_ >>                           | This corresponds to the
                             |                                        | Content-Length header sent by the
                             |                                        | client. If no such header was sent
                             |                                        | the application server SHOULD set
                             |                                        | this key to the L<Int> type value.

    C<CONTENT_TYPE>          | C<< Str:_ >>                           | This corresponds to the Content-Type
                             |                                        | header sent by the cilent. If no
                             |                                        | such header was sent the application
                             |                                        | server SHOULD set this key to the
                             |                                        | L<Str> type value.

    C<HTTP_*>                | C<< Str:_ >>                           | The remaining request headers are
                             |                                        | placed here. The names are prefixed
                             |                                        | with C<HTTP_>, in ALL CAPS with the
                             |                                        | hyphens ("-") turned to underscores
                             |                                        | ("_"). Multiple incoming headers
                             |                                        | with the same name should be joined
                             |                                        | with a comma (", ") as described in
                             |                                        | L<RFC 2616|http://www.ietf.org/rfc/rfc2616>.
                             |                                        | The C<HTTP_CONTENT_LENGTH> and
                             |                                        | C<HTTP_CONTENT_TYPE> headers MUST
                             |                                        | NOT be set.

    Other CGI Keys           | C<< Str:_ >>                           | The server SHOULD attempt to provide
                             |                                        | as many other CGI variables as
                             |                                        | possible, but no others are required
                             |                                        | or formally specified.

    C<p6sgi.version>         | C<< Version:D >>                       | This is the version of this
                             |                                        | specification, C<v0.6.Draft>.

    C<p6sgi.url-scheme>      | C<< Str:D >>                           | Either "http" or "https".

    C<p6sgi.input>           | C<< Supply:D >>                        | The input stream for reading the
                             |                                        | body of the request, if any.

    C<p6sgi.errors>          | C<< Supply:D >>                        | The error stream for logging.

    C<p6sgi.ready>           | C<< Promise:D >>                       | This is a vowed Promise that MUST be
                             |                                        | kept by the server as soon as the
                             |                                        | server has tapped the application's
                             |                                        | output Supply and is ready to
                             |                                        | receive emitted messages. The value
                             |                                        | of the kept Promise is irrelevent.
                             |                                        | The server SHOULD NOT break this
                             |                                        | Promise.

    C<p6sgi.multithread>     | C<< Bool:D >>                          | True if the app may be
                             |                                        | simultaneously invoked in another
                             |                                        | thread in the same
                             |                                        | process.

    C<p6sgi.multiprocess>    | C<< Bool:D >>                          | True if the app may be simultaneously
                             |                                        | invoked in another process.

    C<p6sgi.run-once>        | C<< Bool:D >>                          | True if the server expects the app
                             |                                        | to be invoked only once during the
                             |                                        | life of the process. This is not a
                             |                                        | guarantee.

    C<p6sgi.body.encoding>   | C<< Str:D >>                           | Name of the encoding the server will
                             |                                        | use for any strings it is sent.

=end table

In the environment, either C<SCRIPT_NAME> or C<PATH_INFO> must be set to a non-empty string. When C<REQUEST_URI> is "/", the C<PATH_INFO> SHOULD be "/" and C<SCRIPT_NAME> SHOULD be the empty string. C<SCRIPT_NAME> MUST NOT be set to "/".

For those familiar with Perl 5 PSGI, you may want to take care when working with some of these values. A few look very similar, but are subtly different.

The server or middleware or the application may store its own data in the environment as well. These keys MUST contain at least one dot, SHOULD be prefixed with a unique name.

The following prefixes are reserved for use by this standard:

=item C<p6sgi.> is for P6SGI core standard environment.

=item C<p6sgix.> is for P6SGI standard extensions to the environment.

=head3 2.0.2 The Input Stream

The input stream is set in the C<p6sgi.input> key of the environment. The server MUST provide a L<Supply> that emits L<Blob> objects containing the content of the request payload, if any. When the message payload is completely received, the server MUST call C<done> on the Supply. If there is an error processing the request payload, such as an early termination of the body by the client, the server MUST call C<quit> on the Supply with an appropriate exception.

The C<p6sgi.input> Supply MUST either be an on-demand Supply or the server MUST not begin emitting values to the Supply until after the C<p6sgi.ready> L<Promise> has been kept.

=head3 2.0.3 The Error Stream

The error stream MUST be given in the environment via C<p6sgi.errors>. This MUST be a L<Supply> the server provides emitting errors. The application MAY call C<emit> on the Supply zero or more times, passing any object that may be stringified. The server SHOULD write these log entries to a suitable log file or to STDERR or wherever appropriate. If written to a typical file handle, it should automatically append a newline to each emitted message.

=head3 2.0.4 Application Response

A P6SGI application typically returns a L<Promise>. This Promise is kept with a L<Capture> which contains 3 positional arguments: the status code, the headers, and the message body, respectively.

=item The status code is returned as an integer matching one of the standard HTTP status codes (e.g., 200 for success, 500 for error, 404 for not found, etc.).

=item The headers are returned as a List of Pairs mapping header names to header values.

=item The response payload is returned as a L<Supply> that emits zero or more objects. The server MUST handle any L<Cool> or L<Blob> that are emitted as part of the message payload. In addition to Cool and Blob, servers may also received L<List>s of L<Pair>s to allow applications to embed trailing headers, and L<Associative> objects containing protocol-specific messages and options.

Here's an example of such a typical application:

    sub app(%env) {
        start {
            200, [ Content-Type => 'text/plain' ], Supply.from-list([ 'Hello World' ])
        };
    }

Aside from the typical response, applications are permitted to return any part of the response with a different type of object so long as that object provides a coercion to the required type. Here is another application that is functionally equivalent to the typical example just given:

    sub app(%env) {
        Supply.on-demand(-> $s {
            $s.emit([ 200, [ Content-Type => 'text/plain' ], [ 'Hello World' ]);
            $s.done;
        });
    }

The server will coerce the returned object to a C<Promise>, which is kept with the required Capture. The first two elements are what are normally expected, but the third is just a list. A L<List>, however, coerces to Supply as required.

The server SHOULD NOT assume that the Promise will always be kept and SHOULD handle a broken Promise as appropriate. The server SHOULD assume the Promise has been vowed a MUST NOT try to keep or break the Promise itself.

=head4 2.0.4.0 Response Headers

Each L<Pair> in the list of headers maps a header name to a header value. The application may return the same header name multiple times. The order of multiple headers with the same name SHOULD be preserved.

If the application is missing headers that are required for the status code given or provides headers that are forbidden, the application server SHOULD treat that as a server error.

If given, the server SHOULD examine the C<Content-Type> header for the C<charset> setting. This SHOULD be used to aid in encoding any L<Str> encountered when processing the message payload. If the application does not provide a C<charset>, the server MAY choose to add this header itself using the encoding provided in C<p6sgi.body.encoding> in the environment.

The server SHOULD examine the C<Content-Length> header, if given. It MAY choose to stop consuming the message payload once the number of bytes given has been read.

=head4 2.0.4.1 Response Payload

Unless the status code is one that is not permitted to have a message payload, the application server MUST tap the Supply and process each emitted L<Blob> or L<Cool>, until the the either the Supply is done or the server decides to quit tapping the stream for some reason.

The application server SHOULD continue processing emitted values until the Supply is done or until C<Content-Length> bytes have been emitted. The server MAY stop tapping the Supply for various other reasons as well, such as timeouts or because the client has closed the socket, etc.

If the Supply is quit instead of being done, the server SHOULD attempt to handle the error as appropriate.

=head3 2.0.5 Payload and Encoding

It is up to the server how to handle encoded characters given by the application within the headers.

Within the body, however, any L<Cool> emitted from the L<Supply> MUST be stringified and then encoded. If the application has specified a C<charset> with the C<Content-Type> header, the server SHOULD honor that character encoding. If none is given or the server does not honor the C<Content-Type> header, it MUST encode any stringified Cool with the encoding named in C<psgi.encoding>.

Any L<Blob> encountered in the body SHOULD be sent on as is, treating the data as plain binary.

Any L<List> of L<Pair>s is treated as a trailing header. The details of how this works are protocol-specific.

Any L<Associative> defines a custom message which allows the application to communicate special protocol-specific messages through the server.

=head3 2.0.6 Application Lifecycle

A P6SGI application server processes requests from an origin, passes the processed request information to the application, waits for the application's response, and then returns the response to the origin. In the simplest example this means handling an HTTP roundtrip. It may also mean implementing a related protocol like CGI or FastCGI or SCGI or something else entirely.

In the modern web, an application may want to implement a variety of complex HTTP interactions. These use-cases are not described by the typical HTTP request-response roundtrip. For example, an application may implement a WebSocket API or an interactive Accept-Continue response or stream data to or from the origin. As such, application servers SHOULD make a best effort to be implemented in such a way as to make this variety applications possible.

The application server SHOULD pass control to the application as soon as the headers have been received and the environment can be constructed. The application server MAY continue processing the message body while the application server begins its work. The server SHOULD NOT emit the contents of the request payload via C<p6sgi.input> yet. The server MUST NOT emit to C<p6sgi.input> at this point unless the L<Supply> there is provided on-demand.

Once the application has returned the response headers and the response payload to the server. The server MUST tap the L<Supply> representing the response payload as soon as possible. Immediately after tapping the Supply, the application server MUST keep the L<Promise> (with no value) in C<p6sgi.ready>. The application server SHOULD NOT break this Promise. Immediately after keeping the Promise in C<p6sgi.ready>, the server SHOULD start emitting the contents of the request payload, if any, to C<p6sgi.input>.

The server SHOULD return the application response headers back to the origin as soon as they are received. After which, the server SHOULD return each chunk emitted by the response body from the application as soon as possible.

=head2 2.1 Layer 1: Middleware

P6SGI middleware is a P6SGI application that wraps another P6SGI application. Middleware is used to perform any kind of pre-processing, post-processing, or side-effects that might be added onto an application. Possible uses include logging, encoding, validation, security, debugging, routing, interface adaptation, and header manipulation.

For example, in the following snippet C<&mw> is a simple middleware application that adds a custom header:

    my &app = sub (%env) {
        start {
            200,
            [ Content-Type => 'text/plain' ],
            Supply.from-list([ 'Hello World' ])
        };
    }

    my &mw = sub (%env) {
        callsame().then(-> $p {
            my @res = $p.result;
            @res[1].push: P6SGI-Used => 'True';
            @res;
        });
    };

    &app.wrap(&mw);

B<Note:> For those familiar with PSGI and Plack should take careful notice that Perl 6 C<wrap> has the invocant and argument swapped from the way Plack::Middlware operates. In P6SGI, the C<wrap> method is always called on the I<app> not the I<middleware>.

=head3 2.1.0 Middleware Application

The way middleware is applied to an application varies. There are two basic mechanisms that may be used: the C<wrap> method and the closure method. This is Perl, so there are likely other methods that are possible (since this is Perl 6, some might not be fully implemented yet).

=head4 2.1.0.0 Wrap Method

This is the method demonstrated in the example above. Perl 6 provides a handy C<wrap> method which may be used to apply another subroutine as an aspect of the subroutine being wrapped. In this case, the original application may be called using C<callsame> or C<callwith>.

=head4 2.1.0.1 Closure Method

This method resembles that which would normally be used in PSGI, which is to define the middleware using a closure that wraps the application.

    my &mw = sub (%env) {
        app(%env).then(-> $p {
            my @res = $p.result;
            @res[1].push: P6SGI-Used => 'True';
            @res;
        });
    };
    &app = &mw;

This example is functionality identical to the previous example.

=head3 2.1.1 Environment

Middleware applications SHOULD pass on the complete environment, only modifying the bits required to perform their purpose. Middlware applications MAY add new keys to the environment as a side-effect. These additional keys MUST contain a period and SHOULD use a unique namespace.

=head3 2.1.2 The Input Stream

An application server is required to provide the input stream as a L<Supply> emitting L<Blob>s. Middleware, however, MAY replace the Supply with one that emits anything that might be useful to the application or even remove it altogether.

The middleware MUST still terminate the payload in the Input Stream with a call to C<done> and an abnormal termination with C<quit>.

=head3 2.1.3 The Error Stream

See section 2.2.3.

=head3 2.1.4 Application Response

As with an application, middleware MUST return a valid P6SGI response to the server.

=head3 2.1.5 Payload and Encoding

All the encoding issues in 2.0.5 and 2.2.5 need to be considered.

Special care needs to be taken when middleware needs to process the application body. In such cases, the middleware MUST deal with the possibility of needing to encode the characters being supplied. In such cases, middleware MUST do its best to handle encoding as servers are required:

=item1 Any L<Blob> data SHOULD be passed through as-is.

=item1 Any non-Blob SHOULD be stringified using L<Str> or the unary C<~> prefix.

=item1 Middleware SHOULD examine the charset value of the Content-Type header and prefer that encoding.

=item1 If no charset is present or the middleware does not implementing charset handling, the middleware MUST encode using the value in C<p6sgi.body.encoding>.

The application is permitted to emit any object as part of the response payload so long as some middleware maps those objects into the types the application server is required to support: L<Cool>, L<Blob>, L<List> of L<Pairs>, and L<Associative>. The latter two must be appropriate for the protocol being communicated.

=head2 2.2 Layer 2: Application

A P6SGI application is a Perl 6 routine that receives a P6SGI environment and responds to it by returning a response.

A simple Hello World P6SGI application may be implemented as follows:

    sub app(%env) {
        start {
            200, [ Content-Type => 'text/plain' ], [ 'Hello World' ]
        };
    }

=head3 2.2.0 Defining an Application

The way an application is defined and used by a P6SGI server may vary by server. The specification does not tell servers how to locate an application to run it, so you will need to see the server's documentation for a description of how to do that.

Typically, however, the application is defined in a script file that defines the application subroutine as the final statement in the file (or includes a reference to the application as the final line of the file).

For example, such a file might look like this:

    use v6;
    use MyApp;
    use MyApp::Middleware;
    sub app(%env) {
        my $app = MayApp.new;
        my @body = $app.run;

        200, [ Content-Type => 'text/plain' ], @body.item
    }
    &app.wrap(&my-middleware);
    &app;

In this example, we load some libraries from our imaginary main application, we define a simple P6SGI app, we apply some middleware (presumably exported from C<MyApp::Middleware>), and then end with the reference to our application. This is the typical method by which an application server will load an application.

It is recommended that such files identify themselves with the suffix F<.p6w> when a file suffix is useful.

=head3 2.2.1 The Environment

The one and only argument passed to the application is an L<Associative> containing the environment. The environment variables that MUST be set are defined in section 2.0.1. Additional variables are probably defined by your application server, so please see its documentation for details.

The application MAY store additional values in the environment as it sees fit. This allows the application to communicate with a server or middleware or just to store information useful for the duration of the request. If the application modifies the environment, the variables set MUST contain a period and SHOULD start with a unique name that is not C<p6sgi.> or C<p6sgix.> as these are reserved.

=head3 2.2.2 The Input Stream

During a POST, PUT, or other operation, the client may send along a request payload to your application. The application MAY choose to read the body using the input stream provided in the C<p6sgi.input> key of the environment. This is a L<Supply>, which may emit any kind of object. The server will provide a Supply that emits zero or more L<Blob>s, but the application middleware may map that information as required. It is expected that the application will be written with the kind of data it may receive in mind.

On normal termination of the payload, the Supply will finish with a C<done> signal. On error, it will finish with a C<quit>.

=head3 2.2.3 The Error Stream

The application server is required to provide a C<p6sgi.errors> variable in the environment with a L<Supply> object. The application MAY emit any errors or messages here using any object that stringifies. The application SHOULD NOT terminate such messages with a newline as the server will do so if necessary.

=head3 2.2.4 Application Response

The application MUST return a valid P6SGI response to the server.

A trivial P6SGI application could be implemented like this:

    sub app(%env) {
        start {
            200,
            [ Content-Type => 'text/plain' ],
            [ "Hello World" ],
        };
    }

In detail, an application MUST return a L<Promise> or an object that may coerce into a Promise (i.e., it has a C<Promise> method that takes no arguments and returns a Promise object). This Promise MUST be kept with a Capture or object that coerces into a Capture (e.g., a L<List> or an L<Array>). It MUST contain 3 positional arguments, which are, respectively, the status code, the list of headers, and the message body. These are each defined as follows:

=item The status code MUST be an L<Int> or object that coerces to an Int. It MUST be a valid HTTP status code.

=item The headers MUST be a L<List> of L<Pair>s naming the headers to the application intends to return. The application MAY return the same header name multiple times.

=item The message body MUST be a L<Supply> that typically emits L<Cool> and L<Blob> and L<List> of L<Pair> and L<Associative> objects or an object that coerces into such a Supply (e.g., a List or an Array).

For example, here is another example that demonstrates the flexibility possible in the application response:

    sub app(%env) {
        start {
            my $n = %env<QUERY_STRING>.Int;
            200,
            [ Content-Type => 'text/plain' ],
            Supply.on-demand(-> $content {
                my $acc = 1.FatRat;
                for 1..$n {
                    $content.emit("{$acc *= $n}\n");
                }
                $content.done;
            });
        };
    }

This application will print out all the values of factorial from 1 to N where N is given as the query string. The header is returned immediately, but the lines of the body are returned as the values of factorial are calculated.

The application MUST return this Promise as soon as possible. It is recommended that applications wrap themselves in a C<start {}> block to make this automatic.

=head3 2.2.5 Payload and Encoding

The application may emit any object to the returned L<Supply> to be part of the response payload. It is expected, however, that either these objects will be mapped into the expectations of the application server by middleware or already be in such a form.

The server is expected to process the following emitted objects:

=item L<Blob>. When sending the response payload to the server, the application SHOULD prefer to use Blob objects for the main data payload. This allows the application to fully control the encoding of any text being sent.

=item L<Cool>. It is also possible for the application to use Cool instances, but this puts the server in charge of stringifying and encoding the response. The server is only required to encode the data according to the encoding specified in the C<p6sgi.body.encoding> key of the environment. Application servers and middleware recommended to examine the C<charset> of the Content-Type header returned by the application, but are not required to do so.

=item L<List> of L<Pair>s. Some response payloads may require an additional set of trailing headers. This allows for additional headers to be sent after the payload.

=item L<Associative>. Some web protocols require custom options and messages. These are passed from application to server using Associative objects (usually a L<Hash>).

Applications SHOULD avoid characters that require encoding in HTTP headers.

=head1 3 Extensions

In addition to the standard specification, there are a number of extensions that servers or middleware MAY choose to implement. They are completely optional and applications and middleware SHOULD check for their presence before depending on them.

=head2 3.0 Header Done

The C<p6sgix.header.done> environment variable, if provided, MUST be a vowed L<Promise>. This Promise MUST be kept when the server is done sending or processing the response header. The Promise MUST be broken if the server is unable to or will not send the application provided headers.

This is not an exhaustive list, but here are a few possible reasons why this Promise MAY be broken:

=item The headers are invalid and the application server will not send them.

=item An internal error occurred in the application server.

=item The client hungup the connection before the headers could be sent.

=head2 3.1 Body Done

The C<p6sgix.body.done> environment variable, if provided, MUST be a vowed L<Promise>. This Promise MUST be kept when the server is done sending or processing the response body. The Promise MUST be broken if the server is unable to or will not send the complete application body.

This is not an exhaustive list, but here are a few possible reasons why this Promise MAY be broken:

See also 3.7.

=item The application server has already transmitted C<Content-Length>, but the application continued to send bytes after that point.

=item The client hungup the connection before it finished sending the response.

=head2 3.2 Raw Socket

The C<p6sgix.io> environment variable, if provided, MUST be the bare metal L<IO::Socket::INET> used to communicate to the client. This is the interface of last resort as it sidesteps the entire P6SGI interface.

If your application requires the use of this socket, please file an issue describing the nature of your application in detail. You may have a use-case that requires revisions to the P6SGI standard to cope with.

=head2 3.3 Logger

The C<p6gix.logger> environment variable, if provided, MUST be a L<Routine> defined with a signature as follows:

    sub (Str:D :$level, Str:D :$message);

When called application MUST provide a C<$level> that is one of: `debug`, `info`, `warn`, `error`, `fatal`.

=head2 3.4 Sessions

The C<p6sgix.session> environment variable, if provided, MUST be an L<Associative> mapping arbitrary keys and values that may be read and written to by an application. The application SHOULD only use L<Str> keys and values. The implementation of persisting this data is up to the application or middleware implementing the session.

The C<p6sgix.session.options> environment variable, if provided, MUST be an L<Associative> mapping implementation-specific keys and values. This allows the application a channel by which to instruct the session handler how to operate.

=head2 3.5 Harikiri Mode

The C<p6sgix.harikiri> environment variable, if provided, MUST be a L<Bool>. If set to C<True> it signals to the application that the server supports harikiri mode, which allows the application to ask the server to terminate the current work when the request is complete.

The C<p6sgix.harikiri.commit> environment variable MAY be set by the application to signal to the server that the current worker should be killed after the current request has been processed.

=head2 3.6 Cleanup Handlers

The C<p6sgix.cleanup> environment variable, if provided, MUST be a L<Bool>. If set to C<True> it tells the application that the server supports running cleanup handlers after the request is complete.

The C<p6sgix.cleanup.handlers> environment variable MUST be provided if the C<p6sgix.cleanup> flag is set. This MUST an L<Array>. The application adds cleanup handlers to the list by putting L<Callable>s into the Array (usually by C<push>ing). Each handler will be given a copy of the C<%env> as the first argument.

If the server supports harikiri mode, it SHOULD allow the cleanup handlers to invoke harikiri mode if they set C<p6sgix.hariki.commit> (see 3.5).

=head2 3.7 Output Block Detection

The C<p6sgix.body.backpressure> environment variable, if provided, MUST be a L<Bool> flag. It is set to C<True> to indicate that the P6SGI server provide response backpressure detection by polling for non-blocking I/O problems. In this case, the server MUST provide the other two environment variables. If C<False> or not defined, the server does not provide these two environment variables.

The C<p6sgix.body.backpressure.supply> environment variable MUST be provided if C<p6sgix.body.backpressure> is C<True>. When provided, it MUST be a live L<Supply> that emits C<True> and C<False> values. C<True> is emitted whenever the server detects a blocked output socket. C<False> is emitted whenever the server detects the previously blocked socket is no longer blocked.

The C<p6sgix.body.backpressure.test> environment variable MUST be provided if C<p6sgix.body.backpressure> is C<True>. When provided, it MUST be a L<Bool> that is C<True> while output is blocked and C<False> otherwise. This can be useful for detecting the initial state before the backpressure supply has emitted any value or just as a way to poll the last known status of the socket.

=head2 3.8 Protocol Upgrade

The C<p6sgix.protocol.upgrade> environment variable MUST be provided if the server implements the protocol upgrade extension. It MUST be set to an L<Array> of protocol names of protocols the server supports.

When the client makes a protocol upgrade request using an C<Upgrade> header, the application MAY request that the server upgrade to one of these supported protocols by sending a C<P6SGIx-Upgrade> header back to the server with the named protocol. The application MAY send any other headers related to the Upgrade and MAY send a message payload if the upgrade allows it. These SHOULD override any server supplied values or headers.

The server MUST negotiate the new protocol and enable any environment variables required for interacting through that protocol.

Aside from the protocols named here, additional upgrade protocols may be added by other specifications or implementations. However, the common rule all such upgrades follow is that the application MUST complete work on the current protocol (generally HTTP/1.1) in the current method call.

The server MUST make a new call to the application with a new environment to start processing on the new protocol as is appropriate for that protocol. This lets the application reliably process the activity for a single protocol interaction per subroutine call safely whether an upgrade is performed at the application's request or the protocol is otherwise initiated by the server (e.g., an HTTP/2 request may be initiated by a user agent without an upgrade from HTTP/1.1 or a server MAY automatically perform these upgrades in some or all circumstances depending on implementation).

=head3 3.8.0 HTTP/2 Protocol Upgrade

The workings of HTTP/2 are similar enough to HTTP/1.0 and HTTP/1.1 that use of a protocol upgrade may not be necessary in most or all use-cases. However, servers MAY choose to delegate this to the application using the protocol upgrade extension.

Servers that support this protocol upgrade MUST place the name "h2c" and/or "h2" into the C<p6sgix.protocol.upgrade> array, for support of HTTP/2 over cleartext connections and HTTP/2 over TLS, respectively.

The application MUST NOT tap the C<p6sgix.input> stream when performing this upgrade. The application SHOULD NOT return a message payload aside from an empty L<Supply>.

Once upgraded the application server MUST adhere to all the requirements for HTTP/2 as described in section 4.2. The application will be called again once a web request is received on the upgraded connection and is ready for processing.

=head3 3.8.1 WebSocket Protocol Upgrade

Servers that support the WebSocket protocol upgrade MUST place the name "websocket" into the C<p6sgix.protocol.upgrade> array.

The application MUST NOT tap the C<p6sgix.input> stream when performing this upgrade. The application SHOULD NOT return a message payload aside from an empty L<Supply>.

Once upgraded the application server MUST adhere to all the requirements for WebSocket as described in section 4.3. The application will be called again immediately after the upgrade is complete to allow it to begin sending and receiving frames.

Once upgraded the application

=head2 3.9 Transfer Encoding

This extension is only for HTTP/1.1 protocol connections. When the server supporst this extension, it MUST provide a C<p6sgix.http11.transfer-encoding> variable listing the transfer encodings the server supports.

When the application returns a header named C<P6SGIx-Transfer-Encoding> with the name of one of the supported transfer encodings, the server MUST apply that transfer encoding to the message payload. If the connection is not HTTP/1.1, the server SHOULD ignore this header.

=head3 3.9.0 Chunked Encoding

When the server supports and the application requests "chunked" encoding. The application server MUST treat each emitted L<Str> or L<Blob> as a chunk to be encoded according to L<RFC7230|https://tools.ietf.org/html/draft-ietf-httpbis-p1-messaging>.

=head3 3.9.1 Other Encodings

All other encodings should be handled as required by the relevant rules for HTTP/1.1.

=head1 4 Protocol Implementation

The goal of a P6SGI application server is to allow the application to operate on one of several different web communication protocols without having to deal with all of the mundane of implementing a web server. The reason this requires a standard rather than just an implementation is because we admit at the start that there are many different ways to go about interfacing an application to a server. Some are different ways of communicating, e.g., CGI versus FastCGI versus HTTP. Some are related to implementation and optimization, e.g., a new application server implementation may outperform an older one. Part of the first goal of this specification is to allow application developers to write the application once and allow the application to run on any P6SGI application server with no (or at least very few) changes required.

Therefore, what is meant by "Protocol" in this section of the P6SGI standard is the ultimate protocol the application server is communicating. For example, a typical CGI application works according to the Common Gateway Interface specification, but is ultimately communicating via HTTP/1.0 (or at least that's the only applicable standard in the typical case).

This section defines the additional per-protocol requirements a P6SGI server SHOULD implement for a given protocol. This section will avoid repeating the requirements given in the RFC for each protocol and focus in on the server's responsibility to the application instead.

=head2 4.0 HTTP/1.0

It is generally expected, but not required, that most or all application servers will implement HTTP/1.0. This assumption is so integral to the specification that nearly all the requirements stated in section 2 could be considered the protocol requirements of HTTP/1.0. No additional requirements need to be stated here.

=head2 4.1 HTTP/1.1

Most application servers will implement some aspects of HTTP/1.1. Again, this assumption is integral enough that most HTTP/1.1 protocol details are already mentioned above in section 2. No additional requirements need to be stated here.

=head2 4.2 HTTP/2

Any application server implementing HTTP/2 MUST adhere to the requirements described above with the following additional requirements:

=item The C<SERVER_PROTOCOL> MUST be set to "HTTP/2".

=item The environment MUST contain a variable named C<p6sgix.h2.push-promise>. This MUST be a L<Supply> that the application MAY use to initiate any C<PUSH_PROMISE> frames related to the current response.

=item The server MUST decode incoming frames and only deliver the data contained within the frames to the application in the environment with the same semantics applied as if each incoming stream were an HTTP/1.1 request.

=item The server MUST encode outgoing data emitted by the application into HTTP/2 frames as appropriate.

=head3 4.3 WebSocket

Any application server implementing WebSocket MUST adhere to all the requirements described above with the following modifications:

=item The C<SERVER_PROTOCOL> MUST be set to "WebSocket/13".

=item The server MUST decode frames received from the client and emit them each to C<p6sgix.input>. The frames MUST NOT be buffered or concatenated.

=item The server MUST encode frames emitted by the application in the message payload as data frames sent to the client. The frames MUST be separated out as emitted by the application without any buffering or concatenation.

=head1 Changes

=head2 0.6.Draft

=item Added Protocol-specific details and modifications to the standard HTTP/1.x environment.

=item Adding the Protocol Upgrade extension and started details for HTTP/2 and WebSocket upgrade handling.

=item Adding the Transfer Encoding extension because leaving this to the application or unspecified can lead to tricky scenarios.

=head2 0.5.Draft

=item Adding C<p6sgi.ready> and added the Application Lifecycle section to describe the ideal lifecycle of an application.

=item Changed C<p6sgi.input> and C<p6sgi.errors> to Supply objects.

=item Porting extensions from PSGI and moving the existing extensions into the extension section.

=item Adding some notes about middleware encoding.

=item Renamed C<p6sgi.encoding> to C<p6sgi.body.encoding>.

=item Renamed C<p6sgix.response.sent> to C<p6sgix.body.done>.

=item Added C<p6sgix.header.done> as a new P6SGI extension.

=head2 0.4.Draft

=item Cutting back on some more verbose or unnecessary statements in the standard, trying to stick with just what is important and nothing more.

=item The application response has been completely restructured in a form that is both easy on applications and easy on middleware, mainly taking advantage of the fact that a List easily coerces into a Supply.

=item Eliminating the P6SGI compilation unit again as it is no longer necessary.

=item Change the Supply to emit Cool and Blob rather than just Str and Blob.

=head2 0.3.Draft

=item Splitting the standard formally into layers: Application, Server, and Middleware.

=item Bringing back the legacy standards and bringing back the variety of standard response forms.

=item Middleware is given a higher priority in this revision and more explanation.

=item Adding the P6SGI compiliation unit to provide basic tools that allow middleware and possibly servers to easily process all standard response forms.

=item Section numbering has been added.

=item Added the Changes section.

=item Use C<p6sgi.> prefixes in the environment rather than C<psgi.>

=head2 0.2.Draft

This second revision eliminates the legacy standard and requires that all P6SGI responses be returned as a L<Promise>. The goal is to try and gain some uniformity in the responses the server must deal with.

=head2 0.1.Draft

This is the first published version. It was heavily influenced by PSGI and included interfaces based on the standard, deferred, and streaming responses of PSGI. Instead of callbacks, however, it used L<Promise> to handle deferred responses and L<Channel> to handle streaming. It mentioned middleware in passing.

=end pod
